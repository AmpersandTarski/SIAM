CONTEXT "DeveloperIfc" IN ENGLISH

--[Optional: Main Developer Entry Point]

--#IF MainDevIfc
-- This is *the* entry point for developers. It should only be compiled for the TText test application itself.
-- To allow other applications to provide their own developer's entry point, this section is not compiled by default.

INTERFACE "[Dev]" FOR Developer: "_SESSION"[SESSION] cRud BOX <TABS>
[ "SESSION": I INTERFACE "[SESSION]"
, "SIAMv4":  I INTERFACE "[SIAMv4_Developer]"
]
--#ENDIF

--[SIAMv4 Current Session Interface]

POPULATION isInvisible CONTAINS [ ("[SESSION]", "[SESSION]") ]
INTERFACE "[SESSION]" FOR Developer: "_SESSION" cRud BOX
[ "Session ID": I cRud
, "Session properties": I cRud BOX <TABLE>
   [ "AllowedRoles": sessionAllowedRoles cRud
   , "ActiveRoles": sessionActiveRoles cRud
   , "IsAnon": sessionIsAnon cRud
   , "IsUser": sessionIsUser cRud
   , "IfcsSet": sessionIfcsSet cRud
   , "AnonIfc": sessionAnonIfc cRud
   , "ActorIfc": sessionUserIfc cRud
   ]
, "Account": I cRud BOX <TABLE>
   [ "Account": sessionAccount cRud
   , "OrigAcc": sessionOrigAcc cRud
   , "Userid": sessionUserid cRud
   , "ActorRef": sessionActorRef
   , "PartyRef": sessionPartyRef
   ]
, "Actor, Party": I cRud BOX <TABLE>
   [ "ActorRef": sessionActorRef cRud
   , "Actor": sessionActor cRud
   , "PartyRef": sessionPartyRef cRud
   , "Party": sessionParty cRud
   ]
, "Org, Person": I cRud BOX <TABLE>
   [  "Personal": (sessionActor;I[AorP] /\ sessionParty;I[AorP]);V[Person*Yes]
   ,  "Organizational": (sessionActor;I[AorP] /\ sessionParty;I[AorP]);(I[AorP]-I[Person]);V[AorP*Yes]
   ]
]

--[SIAMv4 Development Interface]

VIEW AccountId:      Account      { "id": I } ENDVIEW
VIEW PartyId:        Party        { "id": I } ENDVIEW
VIEW ActorId:        Actor        { "id": I } ENDVIEW
VIEW PersonId:       Person       { "id": I } ENDVIEW
VIEW OrganizationId: Organization { "id": I } ENDVIEW
VIEW RoleId:         Role         { "id": I } ENDVIEW

POPULATION Yes CONTAINS [ "Yes" ]
VIEW PrsYesNo: Person       { "Yes": V[Person*Yes]       } ENDVIEW
VIEW OrgYesNo: Organization { "Yes": V[Organization*Yes] } ENDVIEW

POPULATION isInvisible CONTAINS [ ("[SIAMv4_Developer]", "[SIAMv4_Developer]") ]
INTERFACE "[SIAMv4_Developer]" FOR Developer: "_SESSION" cRud BOX <TABS>
[ "Accounts": V[SESSION*Account] cRud BOX <TABLE sortable>
   [ "id": I[Account] cRud <AccountId>
   , "Userid": accUserid cRud
-- , "Password": accPassword cRud
   , "ActorRef": accActorRef cRud
   , "Actor": accActor cRud
   , "PartyRef": accPartyRef cRud
   , "Party": accParty cRud
   , "IsInitialized": accIsInitialized cRud
   , "IsActive": accIsActive cRud
   , "UserIfc": accUserIfc cRud
   , "IsGodAccount": accIsGodAccount cRud
   , "AllowedRoles": accAllowedRoles cRud
   , "DefaultRoles": accDefaultRoles cRud
   ]
, "Parties": V[SESSION*Party] cRud BOX <TABLE sortable>
   [ "id": I[Party] cRud <PartyId>
   , "PartyRef": partyRef cRud
   , "Owns (Actors)": isOwnedBy~ cRud
   , "Person?": I[Person] cRud <PrsYesNo>
   , "Organization?": I[Organization] cRud <OrgYesNo>
   ]
, "Actors": V[SESSION*Actor] cRud BOX <TABLE sortable>
   [ "id": I[Actor] cRud <ActorId>
   , "ActorRef": actorRef cRud
   , "Owned by (Party)": isOwnedBy cRud
   , "Person?": I[Person] cRud <PrsYesNo>
   ]
, "Persons": V[SESSION*Person] cRud BOX <TABLE sortable>
   [ "id": I[Person] cRud <PersonId>
   , "Ref": personRef cRud
   , "FirstName": personFirstName cRud
   , "MiddleName": personMiddleName cRud
   , "LastName": personLastName cRud
   , "Initials": personInitials cRud
   , "FullName": personFullName cRud
   , "Pers. Account": (accActor;I[Person] /\ accParty;I[Person])~ cRud
   , "Org. Accounts": (accActor;I[Person] -  accParty;I[Person])~ cRud
   ]
, "Organizations": V[SESSION*Organization] cRud BOX <TABLE sortable>
   [ "id": I[Organization] cRud <OrganizationId>
   , "Ref": orgRef cRud
   , "FullName": orgFullName cRud
   , "Accounts": accParty~ cRud
   ]
, "Roles": V[SESSION*Role] cRud BOX <TABLE sortable>
   [ "id": I[Role] cRud <RoleId>
   , "systemRole": systemRole cRud
   , "Accounts": I[Role] cRud BOX <TABLE sortable>
      [ "Allowed for": accAllowedRoles~ cRud
      , "Default for": accDefaultRoles~ cRud
      ]
   , "SESSION": I[Role] cRud BOX <TABLE sortable>
      [ "Allowed": sessionAllowedRoles~;"_SESSION";V[SESSION*Yes] cRud
      , "Active": sessionActiveRoles~;"_SESSION";V[SESSION*Yes] cRud
      ]
   ]
]

ENDCONTEXT