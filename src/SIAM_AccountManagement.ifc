CONTEXT "AccountAndRoleMgt" IN ENGLISH

POPULATION Role CONTAINS [ "AccAdmin" ] -- Role that allows management of all accounts of this system

ROLE ExecEngine MAINTAINS "De-Assign role `AccAdmin`"
RULE "De-Assign role `AccAdmin`": accAllowedRoles;"AccAdmin" |- (accIsPartyAdmin \/ accIsSystemAdmin);V
VIOLATION (TXT "{EX} DelPair;accAllowedRoles;Account;", SRC I, TXT ";Role;", TGT I)

ROLE ExecEngine MAINTAINS "Assign role `AccAdmin` to SystemAdmins and PartyAdmins"
RULE "Assign role `AccAdmin` to SystemAdmins and PartyAdmins": 
   accIsPartyAdmin \/ accIsSystemAdmin |- accAllowedRoles;"AccAdmin";accAllowedRoles~
VIOLATION (TXT "{EX} InsPair;accAllowedRoles;Account;", SRC I, TXT ";Role;AccAdmin")

--[Accounts that may be managed in this session]
-- EQUIVALENCE sessionManageableAccounts[SESSION*Account] == "_SESSION";sessionAccount;(I \/ accIsPartyAdmin;accParty;accParty~ \/ accIsSystemAdmin;V)
sessionManageableAccounts :: SESSION * Account -- Account that the sessionUser may manage
ROLE ExecEngine MAINTAINS "Equivalence - InsPair sessionManageableAccounts"
RULE "Equivalence - InsPair sessionManageableAccounts": "_SESSION";sessionAccount;(I \/ accIsPartyAdmin;accParty;accParty~ \/ accIsSystemAdmin;V) |- sessionManageableAccounts
VIOLATION (TXT "{EX} InsPair;sessionManageableAccounts;SESSION;", SRC I, TXT ";Account;", TGT I)
ROLE ExecEngine MAINTAINS "Equivalence - DelPair sessionManageableAccounts"
RULE "Equivalence - DelPair sessionManageableAccounts": sessionManageableAccounts |- "_SESSION";sessionAccount;(I \/ accIsPartyAdmin;accParty;accParty~ \/ accIsSystemAdmin;V)
VIOLATION (TXT "{EX} DelPair;sessionManageableAccounts;SESSION;", SRC I, TXT ";Account;", TGT I)

--[Resetting a password]
accNewPassword :: Account * Password [UNI]
RULE "Password Reset can only be done in a session that can manage the account":
   accNewPassword |- (V;"_SESSION";sessionManageableAccounts)~

ROLE ExecEngine MAINTAINS "(Re)set the password of an account"
RULE "(Re)set the password of an account": 
   accNewPassword /\ (V;"_SESSION";sessionManageableAccounts)~ |- -V
VIOLATION (TXT "{EX} InsPair;accPassword;Account;", SRC I, TXT ";Password;", TGT I
          ,TXT "{EX} DelPair;accNewPassword;Account;", SRC I, TXT ";Password;", TGT I
          )

--[Account interfaces]

INTERFACE "Account Mgt" FOR "AccAdmin": sessionManageableAccounts cRud BOX <TABLE sortable>
   [ "Userid": I cRud LINKTO INTERFACE "Account"
   , "User": accActorRef cRud
-- , "User": accActor cRud
   , "Party": accPartyRef cRud
-- , "Party": accParty cRud
   , "Allowed roles": accAllowedRoles cRud
   , "Default roles": accDefaultRoles cRud 
--#IF AccountsInterfaceUsesActivationButtons
   , " ": I BOX <RAW>
      [ "Activate" : I-accIsActive BOX <PROPBUTTON> [ "property": accIsActive cRUd ]
      , "Deactivate" : accIsActive BOX <PROPBUTTON> [ "property": accIsActive cRUd ]
      ]
--#ENDIF
   ]
           
INTERFACE "Account" FOR "AccAdmin": -- AccManagers should not be able to change their own roles
   I[Account] /\ sessionManageableAccounts~;"_SESSION";(sessionManageableAccounts-sessionAccount) cRud BOX <FORM>
   [ "Userid": accUserid cRUd
   , "(Re)set password": accNewPassword cRUd -- crUd is needed for Passwords
   , "User": accActorRef cRud
-- , "User": accActor cRUd
   , "Party": accPartyRef cRud
-- , "Party": accParty cRUd
   , "Allowed roles": accAllowedRoles cRUd
   , "Default roles": accDefaultRoles cRUd
   , " ": I BOX <RAW>
      [ "Activate" : I-accIsActive BOX <PROPBUTTON> [ "property": accIsActive cRUd ]
      , "Deactivate" : accIsActive BOX <PROPBUTTON> [ "property": accIsActive cRUd ]
      ]
   ]

INTERFACE "My Account" FOR User: "_SESSION";sessionAccount cRud BOX <FORM>
   [ "Userid": accUserid cRud
   , "New password": accNewPassword cRUd -- crUd is needed for Passwords
   , "User": accActorRef cRUd
-- , "User": accActor cRUd
   , "Party": accPartyRef cRud
-- , "Party": accParty cRUd
   , "Allowed roles": accAllowedRoles cRud
   , "Default roles": accDefaultRoles cRUd
   ]

--[Roles]--

VIEW AccUIDandParty: Account
{ "userid": accUserid, "(": TXT " (", "partyRef": accPartyRef, ")": TXT ")" 
} ENDVIEW

INTERFACE "Roles" FOR "AccAdmin": V[SESSION*Role] CRud BOX <TABLE sortable>
   [ "Role": I cRud
   , "Assigned to": accAllowedRoles~;(I /\ sessionManageableAccounts~;"_SESSION";sessionManageableAccounts) cRud <AccUIDandParty>
   , "Assign to": I[Role] cRud BOX <OBJECTDROPDOWN>
      [ selectfrom: -I;accAllowedRoles~;(I /\ sessionManageableAccounts~;"_SESSION";sessionManageableAccounts) cRud <AccUIDandParty>
      , setrelation: accAllowedRoles~ cRUd -- If the relation is [UNI], a newly selected object will replace the existing population.
      , instruction: TXT "Select account(s) for this role" -- Text that shows when nothing is selected yet
--    , selectflag: selectEventFlag cRUd -- [PROP]-type relation that toggles when OBJECT is selected
--    , deselectflag: deselectEventFlag cRUd -- [PROP]-type relation that toggles when NO OBJECT is selected
      ]
   , "Default for": accDefaultRoles~;(I /\ sessionManageableAccounts~;"_SESSION";sessionManageableAccounts) cRud <AccUIDandParty>
   , "Make default for": I[Role] cRud BOX <OBJECTDROPDOWN>
      [ selectfrom: -I;accDefaultRoles~;(I /\ sessionManageableAccounts~;"_SESSION";sessionManageableAccounts) cRud <AccUIDandParty>
      , setrelation: accDefaultRoles~ cRUd -- If the relation is [UNI], a newly selected object will replace the existing population.
      , instruction: TXT "Select account(s) for this default role" -- Text that shows when nothing is selected yet
--    , selectflag: selectEventFlag cRUd -- [PROP]-type relation that toggles when OBJECT is selected
--    , deselectflag: deselectEventFlag cRUd -- [PROP]-type relation that toggles when NO OBJECT is selected
      ]
   ]

ENDCONTEXT